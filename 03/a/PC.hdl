// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=regOut, out=incRegOut); //inc val
    Mux16(a=regOut, b=incRegOut, sel=inc, out=regOrIncReg);
    Mux16(a=regOrIncReg, b=in, sel=load, out=regOrIn);
    Mux16(a=regOrIn, b[0..15]=false, sel=reset, out=regOrInOr0);
    
    
    Register(in=regOrInOr0, load=true, out=regOut);

    And16(a=regOut, b[0..15]=true, out=out);
}
